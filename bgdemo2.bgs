dim addr(6)

#declare buffer for building attribute value
dim tmp(1)
#variable to save handle
dim batconn_handle
#store battery measurement result
dim batresult

dim weight

# Boot Event listener
event system_boot(major ,minor ,patch ,build ,ll_version ,protocol_version ,hw )
	
	#Get local BT address
	call system_address_get( )(addr(0:6))
	weight=0
	# Write BT address to DI service serial number string
	call attributes_write(xgatt_dis_2a25,0,6,addr(0:5))
	
	
	
	call hardware_io_port_config_direction(1, $20)
	call hardware_io_port_write(1, $20, 0)

	
	#set to advertising mode
	call gap_set_mode(gap_general_discoverable,gap_undirected_connectable)

	#set bondable mode
	call sm_set_bondable_mode(1)
	batconn_handle=$ff
	
	call hardware_set_soft_timer(32768*3,0,0)
	
end

event hardware_soft_timer(handle)    
	weight=weight+1	
	call attributes_write(xgatt_pdata,0,1,weight)
	
end

# Disconnection event listener
event connection_disconnected(handle,result)
	#connection disconnected, continue advertising
	batconn_handle=$ff
    call gap_set_mode(gap_general_discoverable,gap_undirected_connectable)
	call hardware_io_port_write(1, $20, 0)
	
	
end

# Connection event listener
event connection_status(connection, flags, address, address_type, conn_interval, timeout, latency,bonding)
# Device is connected.
	call hardware_io_port_write(1, $20, $20)	
end



##events for battery listening on vd3
event attributes_user_read_request(connection, handle, offset, maxsize)
	batconn_handle=connection
	#start measurement, read VDD/3, 9 effective bits 
	call hardware_adc_read(15,3,0)
end

#ADC event listener
event hardware_adc_result(input,value)
    #scale value to range 0-100
	#measurement range is 32768=1.15v*3=3.45V
	#new battery ADC measurement is 20912=2.52v
	#minimum battery voltage is 2.0 volts=2.0v/3.45v*32768= 18996
	#20912-18996=1916
	
	batresult=(value-18995)*100/1916
	
	#clip to 100%
	if batresult>100 then
		batresult=100
	end if
	if batresult<0 then
	    batresult=0
	end if

	tmp(0:1)=batresult

	if batconn_handle<$ff then
		#if connection handle is valid
		call attributes_user_read_response(batconn_handle,0,1,tmp(0:1))
		batconn_handle=$ff
	end if
end



